cmake_minimum_required (VERSION 3.5)
project ("cc.mqtt5.commsdsl")

option (MQTT5_NO_EXAMPLES "Disable examples compilation." OFF)
option (MQTT5_NO_WARN_AS_ERR "Don't treat compilation warnings as errors." OFF)
option (MQTT5_USE_CCACHE "Use ccache on UNIX." ON)
option (MQTT5_BUILD_GEN_PROTOCOL "Build and install generated protocol" ON)
option (MQTT5_BUILD_GEN_DOC "Build documentation for generated protocol" ${MQTT5_BUILD_GEN_PROTOCOL})
option (MQTT5_BUILD_GEN_TEST "Build and install project generated by the commsdsl2test." OFF)
option (MQTT5_BUILD_GEN_TOOLS "Build and install project generated by the commsdsl2tools_qt." OFF)
option (MQTT5_BUILD_GEN_SWIG "Build and install project generated by the commsdsl2swig." OFF)

# Additional variables to be used if needed
# ---------------------------
# MQTT5_PROT_OUTPUT_DIR - Path to protocol output directory. If not provided <build_dir>/output one is used.
# MQTT5_TEST_OUTPUT_DIR - Path to test output directory. If not provided <build_dir>/output_tools one is used.
# MQTT5_TOOLS_OUTPUT_DIR - Path to tools output directory. If not provided <build_dir>/output_tools one is used.
# COMMSDSL2COMMS_EXTRA_ARGS - Extra arguments to pass to "commsdsl2comms"
# COMMSDSL2TEST_EXTRA_ARGS - Extra arguments to pass to "commsdsl2test"
# COMMSDSL2TOOLS_QT_EXTRA_ARGS - Extra arguments to pass to "commsdsl2tools_qt"
# COMMSDSL2SWIG_EXTRA_ARGS - Extra arguments to pass to "commsdsl2swig"

set (VERSION "2.0.0")

set (EXTERNALS_DIR "${PROJECT_SOURCE_DIR}/externals")

set (PROT_OUTPUT_TGT "prot_output_tgt")

set (schema_file "${CMAKE_SOURCE_DIR}/dsl/schema.xml")

if ("${MQTT5_PROT_OUTPUT_DIR}" STREQUAL "")
    set (MQTT5_PROT_OUTPUT_DIR "${CMAKE_BINARY_DIR}/output")
endif ()

if ("${MQTT5_TEST_OUTPUT_DIR}" STREQUAL "")
    set (MQTT5_TEST_OUTPUT_DIR "${CMAKE_BINARY_DIR}/output_test")
endif ()

if ("${MQTT5_TOOLS_OUTPUT_DIR}" STREQUAL "")
    set (MQTT5_TOOLS_OUTPUT_DIR "${CMAKE_BINARY_DIR}/output_tools")
endif ()

if ("${MQTT5_SWIG_OUTPUT_DIR}" STREQUAL "")
    set (MQTT5_SWIG_OUTPUT_DIR "${CMAKE_BINARY_DIR}/output_swig")
endif ()

set (warn_as_error "--warn-as-err")
if (MQTT5_NO_WARN_AS_ERR)
    set (warn_as_error)
endif ()

include (ExternalProject)
if (MQTT5_BUILD_GEN_PROTOCOL)
    find_program(COMMSDSL2COMMS commsdsl2comms PATH_SUFFIXES bin REQUIRED)        

    set (prot_tmp_dir "${CMAKE_BINARY_DIR}/output.tmp")

    add_custom_target(rm_prot_tmp_tgt
        COMMAND ${CMAKE_COMMAND} -E remove_directory ${prot_tmp_dir}
    )

    set (code_input_param)
    if (EXISTS ${PROJECT_SOURCE_DIR}/src)
        set (code_input_param -c ${PROJECT_SOURCE_DIR}/src)
    endif ()    

    add_custom_command(
        OUTPUT ${prot_tmp_dir}
        DEPENDS ${schema_file} ${src_files} rm_prot_tmp_tgt
        COMMAND ${COMMSDSL2COMMS} ${warn_as_error} -V ${VERSION} ${code_input_param}
            -o ${prot_tmp_dir} 
            ${COMMSDSL2COMMS_EXTRA_ARGS}  ${schema_file}
    )

    set (readme_file "${prot_tmp_dir}/README.md")
    add_custom_command(
        OUTPUT ${readme_file}
        DEPENDS ${prot_tmp_dir} 
        COMMAND ${CMAKE_COMMAND} -E copy ${CMAKE_SOURCE_DIR}/readme.generated ${readme_file}
    )

    add_custom_target(${PROT_OUTPUT_TGT} ALL
        COMMAND ${CMAKE_COMMAND}
            -DGENERATED="${prot_tmp_dir}" -DOUTPUT="${MQTT5_PROT_OUTPUT_DIR}"
            -P "${CMAKE_CURRENT_LIST_DIR}/CopyGenerated.cmake"
        DEPENDS ${prot_tmp_dir} ${readme_file} "${CMAKE_CURRENT_LIST_DIR}/CopyGenerated.cmake" ${schema_file})

    execute_process(
        COMMAND ${CMAKE_COMMAND} -E make_directory ${MQTT5_PROT_OUTPUT_DIR}
    )  

    execute_process(
        COMMAND ${CMAKE_COMMAND} -E touch ${MQTT5_PROT_OUTPUT_DIR}/dummy.txt
    )

    set (prot_build_dir ${CMAKE_CURRENT_BINARY_DIR}/prot_tgt_build)
    ExternalProject_Add(
        "prot_tgt"
        BUILD_ALWAYS TRUE
        DEPENDS ${PROT_OUTPUT_TGT}
        SOURCE_DIR ${MQTT5_PROT_OUTPUT_DIR}
        BINARY_DIR ${prot_build_dir}
        INSTALL_DIR ${CMAKE_INSTALL_PREFIX}
        CMAKE_GENERATOR ${CMAKE_GENERATOR}
        CMAKE_ARGS
            -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER} -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
            -DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}
            -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE} -DCMAKE_EXE_LINKER_FLAGS=${CMAKE_EXE_LINKER_FLAGS}           
            -DOPT_REQUIRE_COMMS_LIB=OFF
            -DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}
    )

    find_package(Doxygen)
    if (MQTT5_BUILD_GEN_DOC AND DOXYGEN_FOUND)
        ExternalProject_Add_Step(
            "prot_tgt" 
            "doc"
            COMMAND ${CMAKE_COMMAND} --build ${prot_build_dir} --target doc_cc_mqtt5
            DEPENDEES "build"
            DEPENDERS "install"
        )
    endif ()
endif ()  

set (combined_prefix_path ${CMAKE_INSTALL_PREFIX} ${CMAKE_PREFIX_PATH})
string (REPLACE ";" "|" combined_prefix_path_str "${combined_prefix_path}")

if (MQTT5_BUILD_GEN_TEST)
    if (NOT MQTT5_BUILD_GEN_PROTOCOL)
        message (FATAL_ERROR "MQTT5_BUILD_GEN_PROTOCOL is required for MQTT5_BUILD_GEN_TEST")
    endif ()

    find_program(COMMSDSL2TEST commsdsl2test PATH_SUFFIXES bin REQUIRED)        

    set (test_tmp_dir "${CMAKE_BINARY_DIR}/output_test.tmp")

    add_custom_target(rm_test_tmp_tgt
        COMMAND ${CMAKE_COMMAND} -E remove_directory ${test_tmp_dir}
    )

    add_custom_command(
        OUTPUT ${test_tmp_dir}
        DEPENDS ${schema_file} ${src_files} rm_test_tmp_tgt
        COMMAND ${COMMSDSL2TEST} ${warn_as_error} ${COMMSDSL2TEST_EXTRA_ARGS}
            -o ${test_tmp_dir} ${schema_file}
    )

    add_custom_target("test_output_tgt" ALL
        COMMAND ${CMAKE_COMMAND}
            -DGENERATED="${test_tmp_dir}" -DOUTPUT="${MQTT5_TEST_OUTPUT_DIR}"
            -P "${CMAKE_CURRENT_LIST_DIR}/CopyGenerated.cmake"        
        DEPENDS ${test_tmp_dir} "${CMAKE_CURRENT_LIST_DIR}/CopyGenerated.cmake" ${schema_file})

    execute_process(
        COMMAND ${CMAKE_COMMAND} -E make_directory ${MQTT5_TEST_OUTPUT_DIR}
    )

    execute_process(
        COMMAND ${CMAKE_COMMAND} -E touch ${MQTT5_TEST_OUTPUT_DIR}/dummy.txt
    )

    set (test_build_dir ${CMAKE_CURRENT_BINARY_DIR}/test_tgt_build)
    ExternalProject_Add(
        "test_tgt"
        BUILD_ALWAYS TRUE
        DEPENDS "prot_tgt"
        SOURCE_DIR ${MQTT5_TEST_OUTPUT_DIR}
        BINARY_DIR ${test_build_dir}
        CMAKE_GENERATOR ${CMAKE_GENERATOR}
        LIST_SEPARATOR |
        CMAKE_ARGS
            -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER} -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
            -DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}
            -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE} -DCMAKE_EXE_LINKER_FLAGS=${CMAKE_EXE_LINKER_FLAGS}           
            -DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}
            -DCMAKE_PREFIX_PATH=${combined_prefix_path_str}
    )   
endif ()


if (MQTT5_BUILD_GEN_TOOLS)
    if (NOT MQTT5_BUILD_GEN_PROTOCOL)
        message (FATAL_ERROR "MQTT5_BUILD_GEN_PROTOCOL is required for MQTT5_BUILD_GEN_TOOLS")
    endif ()

    find_program(COMMSDSL2TOOLS_QT commsdsl2tools_qt PATH_SUFFIXES bin REQUIRED)    

    set (tools_tmp_dir "${CMAKE_BINARY_DIR}/output_tools.tmp")

    add_custom_target(rm_tools_tmp_tgt
        COMMAND ${CMAKE_COMMAND} -E remove_directory ${tools_tmp_dir}
    )

    add_custom_command(
        OUTPUT ${tools_tmp_dir}
        DEPENDS ${schema_file} ${src_files} rm_tools_tmp_tgt
        COMMAND ${COMMSDSL2TOOLS_QT} ${warn_as_error} ${COMMSDSL2TOOLS_QT_EXTRA_ARGS}
            -o ${tools_tmp_dir} ${schema_file}
    )

    add_custom_target("tools_output_tgt" ALL
        COMMAND ${CMAKE_COMMAND}
            -DGENERATED="${tools_tmp_dir}" -DOUTPUT="${MQTT5_TOOLS_OUTPUT_DIR}"
            -P "${CMAKE_CURRENT_LIST_DIR}/CopyGenerated.cmake"        
        DEPENDS ${tools_tmp_dir} ${readme_file} "${CMAKE_CURRENT_LIST_DIR}/CopyGenerated.cmake" ${schema_file})

    execute_process(
        COMMAND ${CMAKE_COMMAND} -E make_directory ${MQTT5_TOOLS_OUTPUT_DIR}
    )    

    execute_process(
        COMMAND ${CMAKE_COMMAND} -E touch ${MQTT5_TOOLS_OUTPUT_DIR}/dummy.txt
    )

    set (tools_build_dir ${CMAKE_CURRENT_BINARY_DIR}/tools_tgt_build)
    ExternalProject_Add(
        "tools_tgt"
        BUILD_ALWAYS TRUE
        DEPENDS "prot_tgt" "tools_output_tgt"
        SOURCE_DIR ${MQTT5_TOOLS_OUTPUT_DIR}
        BINARY_DIR ${tools_build_dir}
        CMAKE_GENERATOR ${CMAKE_GENERATOR}
        LIST_SEPARATOR |
        CMAKE_ARGS
            -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER} -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
            -DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}
            -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE} -DCMAKE_EXE_LINKER_FLAGS=${CMAKE_EXE_LINKER_FLAGS}           
            -DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}
            -DCMAKE_PREFIX_PATH=${combined_prefix_path_str}
    )   
endif ()

if (MQTT5_BUILD_GEN_SWIG)
    if (NOT MQTT5_BUILD_GEN_PROTOCOL)
        message (FATAL_ERROR "MQTT5_BUILD_GEN_PROTOCOL is required for MQTT5_BUILD_GEN_swig")
    endif ()

    find_program(COMMSDSL2SWIG commsdsl2swig PATH_SUFFIXES bin REQUIRED)    

    set (swig_tmp_dir "${CMAKE_BINARY_DIR}/output_swig.tmp")

    add_custom_target(rm_swig_tmp_tgt
        COMMAND ${CMAKE_COMMAND} -E remove_directory ${swig_tmp_dir}
    )

    add_custom_command(
        OUTPUT ${swig_tmp_dir}
        DEPENDS ${schema_file} ${src_files} rm_swig_tmp_tgt
        COMMAND ${COMMSDSL2SWIG} ${warn_as_error} ${COMMSDSL2SWIG_EXTRA_ARGS}
            -c ${CMAKE_CURRENT_SOURCE_DIR}/swig_src -o ${swig_tmp_dir} ${schema_file}
    )

    add_custom_target("swig_output_tgt" ALL
        COMMAND ${CMAKE_COMMAND}
            -DGENERATED="${swig_tmp_dir}" -DOUTPUT="${MQTT5_SWIG_OUTPUT_DIR}"
            -P "${CMAKE_CURRENT_LIST_DIR}/CopyGenerated.cmake"        
        DEPENDS ${swig_tmp_dir} ${readme_file} "${CMAKE_CURRENT_LIST_DIR}/CopyGenerated.cmake" ${schema_file})

    execute_process(
        COMMAND ${CMAKE_COMMAND} -E make_directory ${MQTT5_SWIG_OUTPUT_DIR}
    )    

    execute_process(
        COMMAND ${CMAKE_COMMAND} -E touch ${MQTT5_SWIG_OUTPUT_DIR}/dummy.txt
    )

    set (swig_build_dir ${CMAKE_CURRENT_BINARY_DIR}/swig_tgt_build)
    ExternalProject_Add(
        "swig_tgt"
        BUILD_ALWAYS TRUE
        DEPENDS "prot_tgt" "swig_output_tgt"
        SOURCE_DIR ${MQTT5_SWIG_OUTPUT_DIR}
        BINARY_DIR ${swig_build_dir}
        CMAKE_GENERATOR ${CMAKE_GENERATOR}
        LIST_SEPARATOR |
        CMAKE_ARGS
            -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER} -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
            -DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}
            -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE} -DCMAKE_EXE_LINKER_FLAGS=${CMAKE_EXE_LINKER_FLAGS}        
            -DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}
            -DCMAKE_PREFIX_PATH=${combined_prefix_path_str}
            -DOPT_SWIG_LANGUAGES=python|java|csharp
        INSTALL_COMMAND
            ${CMAKE_COMMAND} -E echo "Skipping install"            
    )   
endif ()

